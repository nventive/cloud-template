#!/usr/bin/env just --justfile

dotnet_version := "9.0.100-rc.1.24452.12"
azd_version := "1.10.3"

set shell := ["bash", "-c"]

# run the project locally (same as "run")
default: run

# starts VS Code with appropriate environment variables to allow for debugging
code: checkdotnet
    #!/usr/bin/env bash
    # Eliminate environment variables introduced by the nix shell that interfere
    # with Aspire debugging in VS Code. See https://github.com/dotnet/aspire/issues/5389 and
    # https://github.com/dotnet/aspire/issues/5475
    export -n DIRENV_DIFF
    export -n NIX_CFLAGS_COMPILE
    export -n NIX_LDFLAGS
    export -n PYTHONPATH
    export -n name
    export -n shell
    code .

# install .NET locally if itâ€™s not there
checkdotnet:
    [ -d .dotnet ] && [[ "$(dotnet --version)" == "{{dotnet_version}}" ]] || just installdotnet 

# install the appropriate .NET version locally
installdotnet:
    [ ! -d .dotnet ] || rm -r .dotnet 
    wget https://dot.net/v1/dotnet-install.sh
    chmod +x dotnet-install.sh
    ./dotnet-install.sh --version {{dotnet_version}} --install-dir .dotnet --no-path
    rm dotnet-install.sh

# run the project locally
run: checkdotnet
    dotnet run --project src/Placeholder.AppHost

# ================================
#         Migrations
# ================================

# Get the current timestamp
timestamp := datetime("%Y%m%d%H%M%S")

# Defines the target directory
target_dir := "src/Placeholder.Migration/Migrations/" + datetime("%Y") + "/" + datetime("%m")

# Generate a migration file with the specified name
create-migration MIGRATION_NAME:
    mkdir -p {{target_dir}}
    sed "s/{timestamp}/{{ timestamp }}/g; s/{name}/{{ MIGRATION_NAME }}/g" src/Placeholder.Migration/Migrations/template.txt > "{{target_dir}}/{{timestamp}}_{{MIGRATION_NAME}}.cs"
    echo "Migration file created at {{target_dir}}/{{timestamp}}_{{MIGRATION_NAME}}.cs"
